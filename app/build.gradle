plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.firebase-perf'
    id 'com.google.firebase.crashlytics'
}

def calculatorCoreVersion = "2.8.1"

import java.text.SimpleDateFormat

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.maxsavteam.newmcalc2"
        minSdkVersion 24

        targetSdkVersion 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode 2026
        versionName "2.8.4"

        multiDexEnabled true

        buildConfigField "String", "COMPILE_DATE", "\"${getBuildDate("yyMMdd")}\""
        buildConfigField "String", "COMPILE_TIME", "\"${getBuildTime()}\""

        buildConfigField "String", "CALCULATOR_CORE_VERSION", "\"$calculatorCoreVersion\""
    }

    buildTypes {
        release {
            resValue 'string', 'app_name', 'MCalc'

            minifyEnabled true
            shrinkResources true
            aaptOptions {
                cruncherEnabled false
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        debug {
            resValue 'string', 'app_name', 'MCalc Dev'

            applicationIdSuffix ".dev"
            debuggable true
            versionNameSuffix "-DEBUG"
        }
    }
    buildToolsVersion '32.0.0'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    namespace 'com.maxsavteam.newmcalc2'
}

def static getBuildDate(String format) {
    return new Date().format(format)
}


static String getBuildTime() {
    def df = new SimpleDateFormat("dd/MM/yyyy HH:mm:ss")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date()) + " UTC"
}

dependencies {
    implementation 'uk.co.samuelwall:material-tap-target-prompt:3.3.2'

    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.24'

    implementation 'com.google.firebase:firebase-core:21.0.0'
    implementation 'com.google.firebase:firebase-perf:20.1.0'
    implementation 'com.google.firebase:firebase-analytics:21.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.11'

    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.core:core:1.8.0'

    implementation 'com.google.android.play:core:1.10.3'

    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.4.2'
    implementation 'androidx.navigation:navigation-ui:2.4.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.preference:preference:1.2.0'

    implementation 'com.github.MaxSavTeam:ExceptionHandler-Library:1.1.2'
    implementation "com.maxsavteam:calculator-core:$calculatorCoreVersion"

    implementation 'org.jetbrains:annotations:23.0.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

}
