plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    //id 'com.google.firebase.firebase-perf'
    id 'com.google.firebase.crashlytics'
}

def calculatorCoreVersion = "3.4.0"

import java.text.SimpleDateFormat

android {
    compileSdkVersion 33

    buildFeatures {
        buildConfig true
    }

    defaultConfig {
        applicationId "com.maxsavteam.newmcalc2"
        minSdkVersion 24

        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode 3009
        versionName "3.4.0"

        multiDexEnabled true

        buildConfigField "String", "COMPILE_DATE", "\"${getBuildDate("yyMMdd")}\""
        buildConfigField "String", "COMPILE_TIME", "\"${getBuildTime()}\""

        buildConfigField "String", "CALCULATOR_CORE_VERSION", "\"$calculatorCoreVersion\""
    }

    buildTypes {
        release {
            resValue 'string', 'app_name', 'MCalc'

            minifyEnabled true
            shrinkResources true
            aaptOptions {
                cruncherEnabled false
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        debug {
            resValue 'string', 'app_name', 'MCalc Dev'

            applicationIdSuffix ".dev"
            debuggable true
            versionNameSuffix "-DEBUG"
        }
    }
    buildToolsVersion '33.0.0'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    namespace 'com.maxsavteam.newmcalc2'
}

def static getBuildDate(String format) {
    return new Date().format(format)
}


static String getBuildTime() {
    def df = new SimpleDateFormat("dd/MM/yyyy HH:mm:ss")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date()) + " UTC"
}

dependencies {
    implementation 'com.google.protobuf:protobuf-java:3.23.2'

    implementation 'uk.co.samuelwall:material-tap-target-prompt:3.3.2'

    implementation 'com.github.haseebazeem15:gif-image-view:1.5'

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'

    implementation platform('com.google.firebase:firebase-bom:32.1.1')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'

    implementation 'androidx.appcompat:appcompat:1.6.1'

    implementation 'com.google.android.play:review:2.0.1'
    implementation 'com.google.android.play:app-update:2.1.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    implementation 'com.github.tingyik90:snackprogressbar:6.4.2'

    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation 'com.github.MaxSavTeam:ExceptionHandler-Library:1.1.2'
    implementation "com.maxsavteam:calculator-core:$calculatorCoreVersion"

    implementation 'org.jetbrains:annotations:23.0.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.11'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

}
