plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.firebase-perf'
    id 'com.google.firebase.crashlytics'
}

import java.text.SimpleDateFormat

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.maxsavteam.newmcalc2"
        minSdkVersion 23

        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        versionCode 2006
        versionName "2.1.3"

        multiDexEnabled true

        buildConfigField "String", "COMPILE_DATE", "\"${getBuildDate("yyMMdd")}\""
        buildConfigField "String", "COMPILE_TIME", "\"${getBuildTime()}\""
        buildConfigField "String", "CoreVersion", "\"v3.7.210302\""
    }

    buildTypes {
        release {
            buildConfigField("Boolean", "ISDEBUG", "false")
            minifyEnabled true
            shrinkResources true
            aaptOptions {
                cruncherEnabled false
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".beta"
            buildConfigField("Boolean", "ISDEBUG", "true")
            debuggable true
            versionNameSuffix "-DEBUG"
        }
    }
    buildToolsVersion '31.0.0 rc3'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

def static getBuildDate(String format) {
    return new Date().format(format)
}


static String getBuildTime() {
    def df = new SimpleDateFormat("dd/MM/yyyy HH:mm:ss")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date()) + " UTC"
}

dependencies {
    implementation 'uk.co.samuelwall:material-tap-target-prompt:3.1.1'
    implementation 'ch.obermuhlner:big-math:2.3.0'

    implementation 'com.google.firebase:firebase-core:18.0.3'
    implementation 'com.google.firebase:firebase-perf:19.1.1'
    implementation 'com.google.firebase:firebase-analytics:18.0.3'
    implementation 'com.google.firebase:firebase-crashlytics:17.4.1'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core:1.3.2'

    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation 'com.github.MaxSavTeam:ExceptionHandler-Library:1.0'
    implementation 'com.github.MaxSavTeam:calculator-core:1.7.2'

    implementation 'org.jetbrains:annotations:20.1.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

}
